# $Id$

# find the Mersenne Twister (try to find the header file dSFMT.h)
#
# JÃ¶rg F. Unger, Institute of Structural Mechanics, Bauhaus-University Weimar, August 2009
#
# variables used by this module (can be also defined as environment variables):
#   MERSENNE_ROOT - preferred installation prefix for searching for mersenne
#   MERSENNE_FIND_STATIC_LIBRARY - searches for static libraries (UNIX only)
#   MERSENNE_DEBUG - print debug messages
#
# variables defined by this module
#   MERSENNE_FOUND - defines whether metis was found or not
#   MERSENNE_INCLUDE_DIR - mersenne include directory
#   MERSENNE_LIBRARIES   - mersenne libraries


# initialize variables
MESSAGE(STATUS "Checking for Mersenne Twister ...")
# check if MERSENNE_ROOT is set
IF(NOT MERSENNE_ROOT AND NOT $ENV{MERSENNE_ROOT} STREQUAL "")
  SET(MERSENNE_ROOT $ENV{MERSENNE_ROOT})
ENDIF(NOT MERSENNE_ROOT AND NOT $ENV{MERSENNE_ROOT} STREQUAL "")

# convert path to unix style path and set search path
IF(MERSENNE_ROOT)
  FILE(TO_CMAKE_PATH ${MERSENNE_ROOT} MERSENNE_ROOT)
  SET(_mersenne_INCLUDE_SEARCH_DIRS ${MERSENNE_ROOT}/include ${MERSENNE_ROOT} ${_mersenne_INCLUDE_SEARCH_DIRS})
  SET(_mersenne_LIBRARIES_SEARCH_DIRS ${MERSENNE_ROOT}/lib ${MERSENNE_ROOT} ${_mersenne_LIBRARIES_SEARCH_DIRS})
ENDIF(MERSENNE_ROOT)

# search for header dSFMT.h
FIND_PATH(MERSENNE_INCLUDE_DIR NAMES dSFMT.h HINTS ${_mersenne_INCLUDE_SEARCH_DIRS})

# search for metis library
IF(UNIX AND MERSENNE_FIND_STATIC_LIBRARY)
  SET(MERSENNE_ORIG_CMAKE_FIND_LIBRARY_SUFFIXES ${CMAKE_FIND_LIBRARY_SUFFIXES})
  SET(CMAKE_FIND_LIBRARY_SUFFIXES ".a")
ENDIF(UNIX AND MERSENNE_FIND_STATIC_LIBRARY)
FIND_LIBRARY(MERSENNE_LIBRARIES NAMES dSFMT HINTS ${_mersenne_LIBRARIES_SEARCH_DIRS})
IF(UNIX AND MERSENNE_FIND_STATIC_LIBRARY)
  SET(CMAKE_FIND_LIBRARY_SUFFIXES ${MERSENNE_ORIG_CMAKE_FIND_LIBRARY_SUFFIXES})
ENDIF(UNIX AND MERSENNE_FIND_STATIC_LIBRARY)

# handle the QUIETLY and REQUIRED arguments
INCLUDE(FindPackageHandleStandardArgs)
FIND_PACKAGE_HANDLE_STANDARD_ARGS(Mersenne DEFAULT_MSG MERSENNE_LIBRARIES MERSENNE_INCLUDE_DIR)

if(MERSENNE_DEBUG)
  MESSAGE(STATUS "MERSENNE_FOUND=${MERSENNE_FOUND}")
  MESSAGE(STATUS "MERSENNE_INCLUDE_DIR=${MERSENNE_INCLUDE_DIR}")
  MESSAGE(STATUS "MERSENNE_LIBRARIES=${MERSENNE_LIBRARIES}")
endif(MERSENNE_DEBUG)

