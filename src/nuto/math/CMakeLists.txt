# $Id$
SET(NuToMath_SOURCES
  FullMatrix.cpp
  Matrix.cpp
  NuToMath.cpp
  MathException.cpp
  SparseMatrix.cpp
  SparseMatrixCSR.cpp
  SparseMatrixCSRGeneral.cpp
  SparseMatrixCSRSymmetric.cpp
  SparseDirectSolverMKLDSS.cpp
  SparseDirectSolverMKLPardiso.cpp
  SparseDirectSolverMUMPS.cpp
)

SET(NuToMath_SWIG_MODULES
  NuToMath
  ModulMatrix
  ModulFullMatrix
  ModulOperator
  ModulSparseMatrix
  ModulSparseSolver
)

# build static library
ADD_LIBRARY(NuToMath STATIC ${NuToMath_SOURCES})
IF(CMAKE_SYSTEM_PROCESSOR STREQUAL "x86_64" )
 SET_TARGET_PROPERTIES(NuToMath PROPERTIES COMPILE_FLAGS "-fPIC")
ENDIF(CMAKE_SYSTEM_PROCESSOR STREQUAL "x86_64" )

#TARGET_LINK_LIBRARIES(NuToMath NuToBase ${Boost_LIBRARIES})
#SET_TARGET_PROPERTIES(NuToMath PROPERTIES LINK_FLAGS -Wl,-z,defs)

# install shared library
INSTALL(TARGETS NuToMath ARCHIVE DESTINATION ${NUTO_LIBRARIES_INSTALL_PATH})

# build python modules
CONFIGURE_FILE(__init__.py ${CMAKE_CURRENT_BINARY_DIR}/__init__.py)
NUTO_INSTALL_PYTHON_FILE(__init__.py ${NUTO_PYTHON_MODULES_INSTALL_PATH}/nuto/math)
FOREACH(NuToMath_SWIG_MODULE ${NuToMath_SWIG_MODULES})
  SET_SOURCE_FILES_PROPERTIES(${NuToMath_SWIG_MODULE}.i PROPERTIES CPLUSPLUS ON)
  SET_SOURCE_FILES_PROPERTIES(${NuToMath_SWIG_MODULE}.i PROPERTIES SWIG_FLAGS "${NuTo_SWIG_FLAGS}")
  SWIG_ADD_MODULE(${NuToMath_SWIG_MODULE} python ${NuToMath_SWIG_MODULE}.i)
  SWIG_LINK_LIBRARIES(${NuToMath_SWIG_MODULE} NuToMath NuToBase "${Boost_LIBRARIES}" ${PYTHON_LIBRARIES})
  IF(MUMPS_FOUND)
    SWIG_LINK_LIBRARIES(${NuToMath_SWIG_MODULE} ${MUMPS_LIBRARIES})
  ENDIF(MUMPS_FOUND)
  IF(MKLSolver_FOUND)
    SWIG_LINK_LIBRARIES(${NuToMath_SWIG_MODULE} ${MKLSolver_LIBRARIES})
  ENDIF(MKLSolver_FOUND)
  SWIG_LINK_LIBRARIES(${NuToMath_SWIG_MODULE} ${CMAKE_THREAD_LIBS_INIT})
  # check for unresolved symbols
  IF( NOT CYGWIN AND NOT WIN32 )
    SET_TARGET_PROPERTIES(${SWIG_MODULE_${NuToMath_SWIG_MODULE}_REAL_NAME} PROPERTIES LINK_FLAGS -Wl,-z,defs)
  ENDIF( NOT CYGWIN AND NOT WIN32 )
  IF(MINGW)
    SET_TARGET_PROPERTIES(${SWIG_MODULE_${NuToMath_SWIG_MODULE}_REAL_NAME} PROPERTIES LINK_FLAGS "-shared -Wl,--enable-auto-import")
  ENDIF(MINGW)
  # install
  NUTO_INSTALL_SWIG_PYTHON_MODULE(${NuToMath_SWIG_MODULE} ${NUTO_PYTHON_MODULES_INSTALL_PATH}/nuto/math)
ENDFOREACH(NuToMath_SWIG_MODULE)
